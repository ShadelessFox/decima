plugins {
    id 'java'
    id 'application'
}

group 'com.shade'
version '0.1.17'

application {
    mainClass = 'com.shade.platform.Launcher'
    executableDir = ''
    applicationDefaultJvmArgs = [
            // Required for UIUtils#minimizePanel
            '--add-opens', 'java.desktop/javax.swing.plaf.basic=ALL-UNNAMED'
    ]
}

distributions {
    main {
        contents {
            from('data') {
                include '**/*.gz'
                into 'data'
            }
        }
    }
}

dependencies {
    implementation project(':platform-model')
    implementation project(':platform-ui')
    implementation project(':decima-model')
    implementation project(':decima-ui')
    implementation project(':decima-ext-model-exporter')
    implementation project(':decima-ext-model-viewer')
    implementation project(':decima-ext-shader-viewer')
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'java-library'

    repositories {
        mavenCentral()
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    dependencies {
        // https://mvnrepository.com/artifact/ch.qos.logback/logback-classic
        implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.4.4'

        // https://mvnrepository.com/artifact/org.junit.jupiter
        testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.9.1'
        testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: '5.9.1'
        testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.9.1'
    }
}